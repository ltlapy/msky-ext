/// @ 0.12.2
// CC0 https://creativecommons.org/publicdomain/zero/1.0/
// 2023-05-09: ì†ŒìŠ¤ ë¯¸ë¦¬ë³´ê¸° ì¶”ê°€
// 2023-05-08: Descriptionë€ì˜ mfmì´ ë’¤ì£½ë°•ì£½ì¸ ë¬¸ì œ í•´ê²°, ì»´í¬ë„ŒíŠ¸ë³„ ë°ëª¨ UI ì¶”ê°€

var selected = 'MainPage'

let Catalog = {
    // index for convenience
    // these will be overrided by later definition
    container:      { method: Ui:C:container }
    text:           { method: Ui:C:text }
    mfm:            { method: Ui:C:mfm }
    textarea:       { method: Ui:C:textarea }
    textInput:      { method: Ui:C:textInput }
    numberInput:    { method: Ui:C:numberInput }
    button:         { method: Ui:C:button }
    buttons:        { method: Ui:C:buttons }
    switch:         { method: Ui:C:switch }
    select:         { method: Ui:C:select }
    folder:         { method: Ui:C:folder }
    postFormButton: { method: Ui:C:postFormButton }
    postForm:       { method: Ui:C:postForm }  // 2023-09-09
}
let RenderedSampleCodes = {}

//region UiTemplates

@buildDescription(description) {
    if (description == null) {
        description = 'No descriptions specified.'
    } elif (Core:type(description) == "obj") {
        // already built
        return description
    }

    if (Core:type(description) == "arr") {
        description = description.join(Str:lf)
    }
    // do nothing on "str"

    return Ui:C:mfm({text: description})
}

@updateComponentDemo(component) {
    var canvas = []
    
    var title = component.title
    var description = component.description

    // title & description
    Ui:get('title').update({ text: title })
    
    description = buildDescription(description)
    Ui:get('descriptionArea').update({ children: [description] })

    // arguments
    if (component.arguments == null) {
        Ui:get('arguments').update({ hidden: true })
    } else {
        // TODO: ë” ì¢‹ì€ ë°©ë²•
        var arglist = []
        
        each (let arg, component.arguments) {
            arglist.push(` - **{arg.name}** : {arg.type}`)
        }

        Ui:get('arguments').update({ hidden: false })
        Ui:get('argumentsBody').update({ text: arglist.join(Str:lf) })
    }

    // demo
    if (component.demoUi != null) {
        Ui:get('demoArea').update({
            children: component.demoUi
            hidden: false
        })
        Ui:get('demoSourceBody').update({
            text: [
                '```aiscript'
                component.source
                '```'
            ].join(Str:lf)
        })
        Ui:get('demoSourceArea').update({ hidden: false })
    } else {
        Ui:get('demoArea').update({ hidden: true })
        Ui:get('demoSourceArea').update({ hidden: true })
    }
}

let BodyUi = [
    Ui:C:text({
        text: "Loading..."
        size: 1.2
        bold: true
    } 'title')
    Ui:C:container({
        borderWidth: 4
        padding: 18
        rounded: true
        bgColor: "var(--bg, #884)"
    } 'demoArea')
    Ui:C:container({
        children: [Ui:C:folder({
            title: "ğŸ§° ì†ŒìŠ¤ ë³´ê¸°"
            children: [Ui:C:mfm({} 'demoSourceBody')]
            opened: false
        })]
        borderWidth: 4
    } 'demoSourceArea')
    Ui:C:container({
        children: []
        padding: 4
        borderWidth: 12
        borderColor: "red"
    } 'descriptionArea')
    Ui:C:container({
        children: [Ui:C:folder({
            title: "arguments"
            children: [Ui:C:mfm({} 'argumentsBody')]
        })]
    } 'arguments')
]

let Heartbeat = @() {
  // https://k.lapy.link/play/9fhu4jtdhw
  let this = {
    msg1: `<small>ğŸ”µ ì˜ ê¹œë¹¡ì„ì´ ë©ˆì¶”ë©´ í˜ì´ì§€ë¥¼ ìƒˆë¡œê³ ì¹¨í•´ ì£¼ì„¸ìš”</small>`
    msg2: `<small>âš« ì˜ ê¹œë¹¡ì„ì´ ë©ˆì¶”ë©´ í˜ì´ì§€ë¥¼ ìƒˆë¡œê³ ì¹¨í•´ ì£¼ì„¸ìš”</small>`
  }

  this.beatC = Ui:C:mfm({})

  let _update = @(text){ this.beatC.update({text: text}) }
  this.beat = @(){
    _update(this.msg1)
    Async:timeout(200 @(){ _update(this.msg2) })
  }
  
  Async:interval(1000 this.beat true)

  return Ui:C:container({
    children: [this.beatC]
    align: 'center'
    bgColor: 'var(--bg, #888)'
    padding: 6
    rounded: true
  })
}

@uiInit() {
    var list = [{
        text: ' -- Main Page -- '
        value: 'MainPage'
    }]
    each (let component, Obj:kvs(Catalog)) {
        component[1].description = buildDescription(component[1].description)

        list.push({
            text: component[1].title  // Ui:C:(...)
            value: component[0]  // Catalog[key]
        })
    }

    Ui:render([
        Heartbeat()
        Ui:C:select({
            label: 'AiScript Ui Components'
            items: list
            default: 'MainPage'
            onChange: @(v) {
                selected = v
                var component = {}

                if (v == 'MainPage') {
                    component = MainPage
                } else { component = Obj:get(Catalog, v) }
                updateComponentDemo(component)
            }
        } 'componentList')
        Ui:C:container({
            children: BodyUi
        } 'Body')
    ])
}

//endregion UiTemplates

//region catalog_details

let MainPage = {
    title: ' -- Main Page -- '
    description: [
        '**AiScript Ui ì»´í¬ë„ŒíŠ¸ ì¼ëŒ ë° ì‚¬ìš© ì˜ˆì œ**'
        ''
        "** - ì°¸ê³ ìë£Œ**"
        `>â„¹ ì´ ì„œë²„ëŠ” **AiScript {Core:v}** ë¥¼ ì‚¬ìš© ì¤‘ì…ë‹ˆë‹¤.`
        "ğŸ”— [**AiScript Playground**](https://aiscript-dev.github.io/aiscript/)"
        "<small>(âš ï¸ ì°¸ê³ : ìµœì‹  Misskeyì˜ AiScript ë²„ì „ê³¼ ë‹¤ë¥¼ ìˆ˜ ìˆìœ¼ë©°, Ui ì»´í¬ë„ŒíŠ¸ ë“±ê³¼ ê°™ì€ Misskeyì˜ í•¨ìˆ˜/ìƒìˆ˜ëŠ” ì‚¬ìš©í•  ìˆ˜ ì—†ìŠµë‹ˆë‹¤.)</small>"
        "ğŸ”— ?[**Misskey Hub** AiScript í˜ì´ì§€](https://misskey-hub.net/ko/docs/for-developers/aiscript/)"
        "ğŸ”— ?[**AiScript** ê°€ì´ë“œ (ì¼ë³¸ì–´)](https://github.com/syuilo/aiscript/blob/master/docs/get-started.md) ?[(ì˜ì–´)](https://github.com/syuilo/aiscript/blob/master/translations/en/docs/get-started.md)"
        "ğŸ”— ?[**AiScript** í‘œì¤€ ë¼ì´ë¸ŒëŸ¬ë¦¬](https://github.com/syuilo/aiscript/blob/master/docs/std.md)"
        "<small>(âš ï¸ ì°¸ê³ :  `Str`, `Arr`ëŠ” Primitive methodsë¡œ ë³€ê²½ë˜ì—ˆì–´ìš”. ?[Commit](https://github.com/syuilo/aiscript/commit/b9018ded5d5ffc47f89fc8d2b228303e1500ae72) / ?[Issue](https://github.com/syuilo/aiscript/issues/208) / ?[PR](https://github.com/syuilo/aiscript/pull/209))</small>"
        "ğŸ”— ?[**AiScript** Misskey í™•ì¥ (Mk:...)](https://github.com/misskey-dev/misskey/blob/develop/packages/frontend/src/scripts/aiscript/api.ts)"
        "ğŸ”— ?[**AiScript** ì†ŒìŠ¤ì½”ë“œ](https://github.com/syuilo/aiscript)"
        "ğŸ”— ?[**AiScript** í‘œì¤€ ë¼ì´ë¸ŒëŸ¬ë¦¬ ì†ŒìŠ¤ì½”ë“œ](https://github.com/syuilo/aiscript/blob/master/src/interpreter/lib/std.ts)"
        "ğŸ”— ?[**AiScript** - Primitive Props (num, str, arr)](https://github.com/syuilo/aiscript/blob/master/src/interpreter/primitive-props.ts)"
        "ğŸ”— ?[**AiScript UI** ì†ŒìŠ¤ ì½”ë“œ (develop ë¸Œëœì¹˜)](https://github.com/misskey-dev/misskey/blob/develop/packages/frontend/src/scripts/aiscript/ui.ts)"
        "ğŸ”— ?[**AiScript UI** Vue Component ì†ŒìŠ¤ ì½”ë“œ (develop ë¸Œëœì¹˜)](https://github.com/misskey-dev/misskey/blob/develop/packages/frontend/src/components/MkAsUi.vue)"
        "ğŸ”— ?[**AiScript** Misskey í”ŒëŸ¬ê·¸ì¸ ì†ŒìŠ¤ ì½”ë“œ](https://github.com/misskey-dev/misskey/blob/develop/packages/frontend/src/plugin.ts)"
    ]
    //showUiOnly: true
}

Catalog.container = {
    title: 'Ui:C:container'
    description: [
        'ì—¬ëŸ¬ ê°œì˜ AiScript Ui êµ¬ì„± ìš”ì†Œë¥¼ ë‹´ì„ ìˆ˜ ìˆëŠ” ì»¨í…Œì´ë„ˆì…ë‹ˆë‹¤.'
        'ì¢Œìš° ì •ë ¬, ë°”íƒ•ìƒ‰, ê¸€ììƒ‰, ê¸€ê¼´ ëª¨ì–‘, í…Œë‘ë¦¬, ì—¬ë°±, ë‘¥ê·¼ ëª¨ì„œë¦¬ ì—¬ë¶€, ë³´ì´ê¸°/ìˆ¨ê¸°ê¸°ì™€ ê°™ì€ ì„¸ë¶€ì ì¸ ì¡°ì ˆì„ í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "children", type: "AsUiComponent['id'][]", optional: "true" }
        { name: "align", type: "'left' | 'center' | 'right'", optional: "true" }
        { name: "bgColor", type: "string", optional: "true" }
        { name: "fgColor", type: "string", optional: "true" }
        { name: "font", type: "'serif' | 'sans-serif' | 'monospace'", optional: "true" }
        { name: "borderWidth", type: "number", optional: "true" }
        { name: "borderColor", type: "string", optional: "true" }
        { name: "padding", type: "number", optional: "true" }
        { name: "rounded", type: "boolean", optional: "true" }
        { name: "hidden", type: "boolean", optional: "true" }
    ]
    demoUi: [
        Ui:C:mfm({ text: [
            "ì´ ë°ëª¨ ì°½ì€ ë‹¤ìŒê³¼ ê°™ì´ ì •ì˜ëœ containerë¥¼ ì´ìš©í•©ë‹ˆë‹¤."
            "```aiscript"
            "Ui:C:container({"
            "    borderWidth: 4"
            "    padding: 18"
            "    rounded: true"
            "    bgColor: 'var(--bg, #884)'"
            "} 'demoArea')'"
            "```"
          ].join(Str:lf) })
    ]
    source: [
        "Ui:C:container({"
        "    children: ["
        "        Ui:C:mfm({ text: ["
        "            \"ì´ ë°ëª¨ ì°½ì€ ë‹¤ìŒê³¼ ê°™ì´ ì •ì˜ëœ containerë¥¼ ì´ìš©í•©ë‹ˆë‹¤.\""
        "            \"```aiscript\""
        "            \"Ui:C:container({\""
        "            \"    borderWidth: 4\""
        "            \"    padding: 18\""
        "            \"    rounded: true\""
        "            \"    bgColor: 'var(--bg, #884)\'\""
        "            \"} 'demoArea')'\""
        "            \"```\""
        "        ].join(Str:lf) })"
        "    ]"
        "    borderWidth: 4"
        "    padding: 18"
        "    rounded: true"
        "    bgColor: 'var(--bg, #884)'"
        "} 'demoArea')"
    ].join(Str:lf)
} // End of Catalog.container
    
Catalog.text = {
    title: 'Ui:C:text'
    description: [
        'í…ìŠ¤íŠ¸ë¥¼ í‰ë¬¸ìœ¼ë¡œ í‘œì‹œí•©ë‹ˆë‹¤.'
        'ê°œí–‰ ë° MFMì€ ë¬´ì‹œë©ë‹ˆë‹¤. í•„ìš”í•  ê²½ìš° `Ui:C:mfm` ì„ ì‚¬ìš©í•˜ì‹­ì‹œì˜¤.'
    ].join(Str:lf)
    arguments: [
        { name: "text", type: "string", optional: "true" }
        { name: "size", type: "number", optional: "true" }
        { name: "bold", type: "boolean", optional: "true" }
        { name: "color", type: "string", optional: "true" }
        { name: "font", type: "'serif' | 'sans-serif' | 'monospace'", optional: "true" }
    ]
    demoUi: [
        Ui:C:text({ text: "I $[jelly â¤] #Misskey" })
    ]
    source: "Ui:C:text({ text: 'I $[jelly â¤] #Misskey' })"
} // End of Catalog.text

Catalog.mfm = {
    title: 'Ui:C:mfm'
    description: [
        'ì„œì‹ ìˆëŠ” í…ìŠ¤íŠ¸ë¥¼ í‘œì‹œí•©ë‹ˆë‹¤.'
        '`Ui:C:text`ì™€ ë‹¬ë¦¬ ê°œí–‰ ë° MFMê¹Œì§€ ëª¨ë‘ í‘œì‹œë©ë‹ˆë‹¤. í•„ìš” ì‹œ `Ui:C:text`ë¥¼ ì‚¬ìš©í•˜ì‹­ì‹œì˜¤.'
    ].join(Str:lf)
    arguments: [
        { name: "text", type: "string", optional: "true" }
        { name: "size", type: "number", optional: "true" }
        { name: "bold", type: "boolean", optional: "true" }
        { name: "color", type: "string", optional: "true" }
        { name: "font", type: "'serif' | 'sans-serif' | 'monospace'", optional: "true" }
    ]
    demoUi: [
        Ui:C:mfm({ text: "I $[jelly â¤] #Misskey" })
    ]
    source: "Ui:C:mfm({ text: 'I $[jelly â¤] #Misskey' })"
} // End of Catalog.mfm

Catalog.button = {
    title: 'Ui:C:button'
    description: [
        'ì‚¬ìš©ìê°€ ìƒí˜¸ì‘ìš©í•  ìˆ˜ ìˆëŠ” ë²„íŠ¼ì…ë‹ˆë‹¤.'
        'ê°•ì¡°, ë‘¥ê¸€ê²Œ, ë¹„í™œì„± ì—¬ë¶€ë¥¼ ì„¤ì •í•  ìˆ˜ ìˆìœ¼ë©°, í´ë¦­í•˜ë©´ ì‚¬ì „ì— ì§€ì •í•œ ì½œë°± í•¨ìˆ˜ë¥¼ í˜¸ì¶œí•©ë‹ˆë‹¤.'
        'í•œ ì¤„ì— ì—¬ëŸ¬ ê°œì˜ ë²„íŠ¼ì„ í‘œì‹œí•˜ë ¤ë©´ `Ui:C:buttons` ë¥¼ ì°¸ê³ í•˜ì‹­ì‹œì˜¤.'
    ].join(Str:lf)
    arguments: [
        { name: "text", type: "string", optional: "true" }
        { name: "onClick", type: "@() {}", optional: "true" }
        { name: "primary", type: "boolean", optional: "true" }
        { name: "rounded", type: "boolean", optional: "true" }
        { name: "disabled", type: "boolean", optional: "true" }
    ]
    demoUi: [
        Ui:C:button({ text: "Primary", primary: true, onClick: @() {Mk:dialog("Alert", "Hello, world!")} })
        Ui:C:button({ text: "Button", onClick: @() {Mk:dialog("Alert", "Hello, world!")}})
        Ui:C:button({ text: "disabled", disabled: true })
    ]
    source: [
        "Ui:C:button({ text: 'Primary', primary: true, onClick: @() {Mk:dialog('Alert', 'Hello, world!')} })"
        "Ui:C:button({ text: 'Button', onClick: @() {Mk:dialog('Alert', 'Hello, world!')}})"
        "Ui:C:button({ text: 'disabled', disabled: true })"
    ].join(Str:lf)
} // End of Catalog.button

Catalog.buttons = {
    title: 'Ui:C:buttons'
    description: [
        'ë²„íŠ¼ì„ í•œ ì¤„ì— ì—¬ëŸ¬ ê°œ í‘œì‹œí•©ë‹ˆë‹¤. `buttons`ì˜ í•˜ìœ„ í•­ëª©ë“¤ì€ `Ui:C:button`ìœ¼ë¡œ ê°ì‹¸ì§€ ì•ŠìŠµë‹ˆë‹¤.'
        'ì˜ë„ëœ ë™ì‘ì¸ ì§€ ì•Œ ìˆ˜ ì—†ìœ¼ë‚˜, `onClick`ì´ ì§€ì •ë˜ì§€ ì•Šì„ ê²½ìš° ì—ëŸ¬ê°€ ë°œìƒí•©ë‹ˆë‹¤.'
        'ìì„¸í•œ ë‚´ìš©ì€ `Ui:C:button` ì„ ì°¸ê³ í•˜ì‹­ì‹œì˜¤.'
    ].join(Str:lf)
    arguments: [
        { name: "buttons", type: "AsUiButton[]", optional: "true" }
    ]
    demoUi: [
        Ui:C:buttons({
            buttons: [
                { text: "Primary", primary: true, onClick: @() {Mk:dialog("Alert", "Hello, world!")} }
                { text: "Button", onClick: @() {Mk:dialog("Alert", "Hello, world!")}}
                { text: "disabled", disabled: true, onClick: @(){} }
                // { text: "disabled", disabled: true }  // - ERROR
            ]
        })
    ]
    source: [
        "Ui:C:buttons({"
        "    buttons: ["
        "        { text: 'Primary', primary: true, onClick: @() {Mk:dialog('Alert', 'Hello, world!')} }"
        "        { text: 'Button', onClick: @() {Mk:dialog('Alert', 'Hello, world!')}}"
        "        { text: 'disabled', disabled: true, onClick: @(){} }"
        "        // { text: 'disabled', disabled: true }  // - ERROR"
        "    ]"
        "})"
    ].join(Str:lf)
} // End of Catalog.buttons

Catalog.switch = {
    title: 'Ui:C:switch'
    description: [
        'êº¼ì§-ì¼œì§ ìƒíƒœë¥¼ ì „í™˜í•  ìˆ˜ ìˆëŠ” ìŠ¤ìœ„ì¹˜ì…ë‹ˆë‹¤.'
        'ê¸°ë³¸ê°’, ë¼ë²¨, ì„¤ëª…ë¬¸ì„ ì„¤ì •í•  ìˆ˜ ìˆìœ¼ë©°, í´ë¦­í•˜ë©´ ì‚¬ì „ì— ì§€ì •í•œ ì½œë°± í•¨ìˆ˜ë¥¼ í˜¸ì¶œí•©ë‹ˆë‹¤.'
        'ë²„íŠ¼ê³¼ ë‹¬ë¦¬ ë¹„í™œì„±í™”í•  ìˆ˜ ì—†ìŠµë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "onChange", type: "@(v: boolean) {}", optional: "true" }
        { name: "default", type: "boolean", optional: "true" }
        { name: "label", type: "string", optional: "true" }
        { name: "caption", type: "string", optional: "true" }
    ]
    demoUi: [
        Ui:C:switch({label: "Switch", caption: "This is demo caption"})
        Ui:C:switch({label: "Default true", default: true})
    ]
    source: [
        "Ui:C:switch({label: 'Switch', caption: 'This is demo caption'})"
        "Ui:C:switch({label: 'Default true', default: true})"
    ].join(Str:lf)

} // End of Catalog.switch

Catalog.textInput = {
    title: 'Ui:C:textInput'
    description: [
        'í•œ ì¤„ í¬ê¸°ì˜ í…ìŠ¤íŠ¸ ì…ë ¥ ì¹¸ì…ë‹ˆë‹¤.'
        'ê¸°ë³¸ê°’, ë¼ë²¨, ì„¤ëª…ë¬¸ì„ ì„¤ì •í•  ìˆ˜ ìˆìœ¼ë©°, ê°’ì´ ìˆ˜ì •ë  ë•Œ ë§ˆë‹¤ ì‚¬ì „ì— ì§€ì •í•œ ì½œë°± í•¨ìˆ˜ë¥¼ í˜¸ì¶œí•©ë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "onInput", type: "@(v: string) {}", optional: "true" }
        { name: "default", type: "string", optional: "true" }
        { name: "label", type: "string", optional: "true" }
        { name: "caption", type: "string", optional: "true" }
    ]
    demoUi: [
        Ui:C:textInput({
            label: "textInput"
            caption: "This is demo textInput"
            default: "Hello, world!"
        })
    ]
    source: [
        "Ui:C:textInput({"
        "    label: 'textInput'"
        "    caption: 'This is demo textInput'"
        "    default: 'Hello, world!'"
        "})"
    ].join(Str:lf)
} // End of Catalog.textInput

Catalog.textarea = {
    title: 'Ui:C:textarea'
    description: [
        'ì—¬ëŸ¬ ì¤„ì„ ì…ë ¥í•  ìˆ˜ ìˆëŠ” í…ìŠ¤íŠ¸ ì…ë ¥ ì¹¸ì…ë‹ˆë‹¤.'
        'ê¸°ë³¸ê°’, ë¼ë²¨, ì„¤ëª…ë¬¸ì„ ì„¤ì •í•  ìˆ˜ ìˆìœ¼ë©°, ê°’ì´ ìˆ˜ì •ë  ë•Œ ë§ˆë‹¤ ì‚¬ì „ì— ì§€ì •í•œ ì½œë°± í•¨ìˆ˜ë¥¼ í˜¸ì¶œí•©ë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "onInput", type: "@(v: string) {}", optional: "true" }
        { name: "default", type: "string", optional: "true" }
        { name: "label", type: "string", optional: "true" }
        { name: "caption", type: "string", optional: "true" }
    ]
    demoUi: [
        Ui:C:textarea({
            label: "Textarea"
            caption: "This is demo textarea"
            default: "Hello, world!"
        })
    ]
    source: [
        "Ui:C:textarea({"
        "    label: 'Textarea'"
        "    caption: 'This is demo textarea'"
        "    default: 'Hello, world!'"
        "})"
    ].join(Str:lf)
} // End of Catalog.textarea

Catalog.numberInput = {
    title: 'Ui:C:numberInput'
    description: [
        'ìˆ«ìë¥¼ ì…ë ¥í•  ìˆ˜ ìˆëŠ” ì¹¸ì…ë‹ˆë‹¤.'
        'ê¸°ë³¸ê°’, ë¼ë²¨, ì„¤ëª…ë¬¸ì„ ì„¤ì •í•  ìˆ˜ ìˆìœ¼ë©°, ê°’ì´ ìˆ˜ì •ë  ë•Œ ë§ˆë‹¤ ì‚¬ì „ì— ì§€ì •í•œ ì½œë°± í•¨ìˆ˜ë¥¼ í˜¸ì¶œí•©ë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "onInput", type: "@(v: number) {}", optional: "true" }
        { name: "default", type: "number", optional: "true" }
        { name: "label", type: "string", optional: "true" }
        { name: "caption", type: "string", optional: "true" }
    ]
    demoUi: [
        Ui:C:numberInput({
            label: "numberInput"
            caption: "This is demo numberInput"
            default: 777
        })
    ]
    source: [
        "Ui:C:numberInput({"
        "    label: 'numberInput'"
        "    caption: 'This is demo numberInput'"
        "    default: 777"
        "})"
    ].join(Str:lf)
} // End of Catalog.numberInput

Catalog.select = {
    title: 'Ui:C:select'
    description: [
        'ì£¼ì–´ì§„ ë¦¬ìŠ¤íŠ¸ ê°€ìš´ë° í•˜ë‚˜ë¥¼ ì„ íƒí•  ìˆ˜ ìˆëŠ” ë“œë¡­ë‹¤ìš´ ë©”ë‰´ì…ë‹ˆë‹¤.'
        'ê¸°ë³¸ê°’, ë¼ë²¨, ì„¤ëª…ë¬¸ì„ ì„¤ì •í•  ìˆ˜ ìˆìœ¼ë©°, ì„ íƒ í•­ëª©ì´ ë³€ê²½ë  ë•Œ ë§ˆë‹¤ ì‚¬ì „ì— ì§€ì •í•œ ì½œë°± í•¨ìˆ˜ë¥¼ í˜¸ì¶œí•©ë‹ˆë‹¤.'
        'ë¦¬ìŠ¤íŠ¸ëŠ” `{ text: string, value: string }` í˜•íƒœì˜ `obj` arrayë¡œ ì •ì˜í•´ì•¼ í•˜ë©°, valueëŠ” ìƒëµí•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "items", type: "{text: string, value?: string}[]", optional: "true" }
        { name: "onChange", type: "@(v: string) {}", optional: "true" }
        { name: "default", type: "string", optional: "true" }
        { name: "label", type: "string", optional: "true" }
        { name: "caption", type: "string", optional: "true" }
    ]
    demoUi: [
        Ui:C:select({
            label: "select"
            caption: "This is demo select"
            items: [
                {text: "Kiwi"}
                {text: "Papaya"}
                {text: "Mango"}
            ]
            default: "Papaya"
        })
    ]
    source: [
        "Ui:C:select({"
        "    label: 'select'"
        "    caption: 'This is demo select'"
        "    items: ["
        "        {text: 'Kiwi'}"
        "        {text: 'Papaya'}"
        "        {text: 'Mango'}"
        "    ]"
        "    default: 'Papaya'"
        "})"
    ].join(Str:lf)
} // End of Catalog.select

Catalog.folder = {
    title: 'Ui:C:folder'
    description: [
        'í´ë¦­í•˜ì—¬ ë‚´ìš©ì„ ë³´ì´ê±°ë‚˜ ìˆ¨ê¸¸ ìˆ˜ ìˆìŠµë‹ˆë‹¤.'
        'AiScript Ui êµ¬ì„±ìš”ì†Œë¥¼ ë‹´ì„ ìˆ˜ ìˆìœ¼ë‚˜, `Ui:C:Container`ì™€ ê°™ì€ íš¨ê³¼ëŠ” ë‹¨ë…ìœ¼ë¡œ ì‚¬ìš©í•  ìˆ˜ ì—†ìŠµë‹ˆë‹¤.'
        'ì œëª©ê³¼ ì—´ë¦¼ ì—¬ë¶€ë¥¼ ì„¤ì •í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "children", type: "AsUiComponent['id'][]", optional: "true" }
        { name: "title", type: "string", optional: "true" }
        { name: "opened", type: "boolean", optional: "true" }
    ]
    demoUi: [
        Ui:C:folder({
            title: "This is sample folder"
            children: [
                Ui:C:mfm({ text: "I $[jelly â¤] #Misskey" })
            ]
        })
    ]
    source: [
        "Ui:C:folder({"
        "    title: 'This is sample folder'"
        "    children: ["
        "        Ui:C:mfm({ text: 'I $[jelly â¤] #Misskey' })"
        "    ]"
        "})"
    ].join(Str:lf)
} // End of Catalog.folder

Catalog.postFormButton = {
    title: 'Ui:C:postFormButton'
    description: [
        'ë…¸íŠ¸ ì‘ì„± í¼ì„ í‘œì‹œí•˜ëŠ” ë²„íŠ¼ì…ë‹ˆë‹¤.'
        'ë²„íŠ¼ì˜ ê¸€ì, ê²Œì‹œí•  í…ìŠ¤íŠ¸, ë‘¥ê¸€ê¸° ì—¬ë¶€, ê°•ì¡° ì—¬ë¶€ë¥¼ ì„¤ì •í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.'
        'ì¼ë°˜ ë²„íŠ¼ê³¼ ë‹¤ë¥´ê²Œ `Ui:C:Buttons`ì™€ í•¨ê»˜ ì‚¬ìš©í•  ìˆ˜ ì—†ìœ¼ë©°, ë¹„í™œì„±í™”í•  ìˆ˜ ì—†ìŠµë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "text", type: "string", optional: "true" }
        { name: "primary", type: "boolean", optional: "true" }
        { name: "rounded", type: "boolean", optional: "true" }
        { name: "form", type: "{text: string}", optional: "true" }
    ]
    demoUi: [
        Ui:C:postFormButton({
            text: "Open postForm"
            primary: true
            form: {text: "I $[jelly â¤] #Misskey"}
        })
    ]
    source: [
        "Ui:C:postFormButton({"
        "    text: 'Open postForm'"
        "    primary: true"
        "    form: {text: 'I $[jelly â¤] #Misskey'}"
        "})"
    ].join(Str:lf)
} // End of Catalog.postFormButton

Catalog.postForm = {
    title: 'Ui:C:postForm'
    description: [
        'ë…¸íŠ¸ ì‘ì„± í¼ì„ í‘œì‹œí•©ë‹ˆë‹¤. **(Misskey 2023.9.0-beta.6 ì´ìƒ)**'
        '`Ui:C:postForm`ê³¼ ë‹¤ë¥´ê²Œ, ê²Œì‹œí•  í…ìŠ¤íŠ¸ì— ëŒ€í•´ì„œë§Œ ì„¤ì •í•  ìˆ˜ ìˆìŠµë‹ˆë‹¤.'
    ].join(Str:lf)
    arguments: [
        { name: "form", type: "{text: string}", optional: "true" }
    ]
    demoUi: [
        Ui:C:postForm({
            form: {text: "I $[jelly â¤] #Misskey"}
        })
    ]
    source: [
        "Ui:C:postForm({"
        "    form: {text: 'I $[jelly â¤] #Misskey'}"
        "})"
    ].join(Str:lf)
} // End of Catalog.postForm

//endregion catalog_details


selected = 'MainPage'
uiInit()
updateComponentDemo(MainPage)
